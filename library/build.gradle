apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def VERSION_NAME = "1.0.7"

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/library-release.aar")
            groupId 'com.mlsdev.androidanimations.library' // replace with your groupId
            artifactId 'AnimationsPack' // replace with your artifactId
            version VERSION_NAME

            pom.withXml {
                //The publication doesn't know about our dependencies, so we have to manually add them to the pom
                // Define this explicitly if using implementation or api configurations
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    // Get Bintray credential from environment variable
    user = System.getenv('BINTRAY_USER') // Get bintray User
    key = System.getenv('BINTRAY_API_KEY') // Get bintray Secret API Key
    dryRun = false
    override = true
    publish = true
    pkg {
        repo = 'AnimationsPack' // replace with your Bintray repo name
        name = 'AnimationsPack' // library module name
        userOrg = System.getenv('BINTRAY_USER') // replace with your Bintray user name
        licenses = ['MIT']
        desc = 'Another implementation of animate.css'
        vcsUrl = 'https://github.com/MLSDev/AnimationsPack.git' //replace with your repo url

        version {
            name = VERSION_NAME
        }
    }
    publications = ['Production']
}

android {
    compileSdkVersion 29


    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER") ?: 1)
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    tasks.withType(Javadoc).all {
        enabled = false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.core:core-ktx:1.0.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core:1.0.2"

    testImplementation 'junit:junit:4.12'
}
repositories {
    mavenCentral()
}
